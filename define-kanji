#!/usr/bin/env perl
use strict;
use warnings;
use Lingua::JP::Kanjidic;
use Encode 'decode_utf8';
my $dic = Lingua::JP::Kanjidic->new("$ENV{HOME}/.kanjidic");

binmode(\*STDOUT, ':utf8');

for my $kanji (grep { /\p{Han}/ } split //, decode_utf8("@ARGV")) {
    my $definition = $dic->lookup($kanji);

    print $kanji;
    if ($definition) {
        my $heisig = $definition->heiseg;
        my $skip = $definition->skip;

        print ' (';
        print $definition->strokes;
        print ' ' if $heisig || $skip;
        print $heisig if $heisig;
        print ' ' if $heisig && $skip;
        print $skip if $skip;
        print ')';

        print ': ';
        print join(', ', @{ $definition->meaning || ["???"] });
    }
    else {
        print ': ???';
    }

    print "\n";
}

