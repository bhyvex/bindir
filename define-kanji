#!/usr/bin/env perl
use strict;
use warnings;
use Lingua::JP::Kanjidic;
use Encode 'decode_utf8';
my $dic = Lingua::JP::Kanjidic->new("$ENV{HOME}/.kanjidic");

binmode(\*STDOUT, ':utf8');

my %is_sought = map { $_ => 1 }
                grep { /\p{Han}/ }
                split //,
                decode_utf8("@ARGV");

while (my $kanji = $dic->next) {
    my $character = $kanji->kanji;
    next if !$is_sought{$character};
    delete $is_sought{$character};

    print $character;
    print ' (' . $kanji->heiseg . ')' if $kanji->heiseg;
    print ': ';
    print join(', ', @{ $kanji->meaning || ["???"] });
    print "\n";

    last if keys %is_sought == 0;
}

for my $kanji (keys %is_sought) {
    print "$kanji: ???\n";
}

